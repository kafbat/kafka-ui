plugins {
  alias(libs.plugins.node.gradle)
}

node {
  download = !"true".equals(project.property("local_node"))
  version = project.property("node_version").toString()
  pnpmVersion = project.property("pnpm_version").toString()
  nodeProjectDir = project.layout.projectDirectory
}


tasks.named("pnpmInstall") {
  inputs.files(file("package.json"))
  outputs.dir(project.layout.projectDirectory.dir("node_modules"))
}

tasks.register('generateContract', PnpmTask) {
  dependsOn pnpmInstall
  inputs.files(fileTree("../contract/src/main/resources"))
  outputs.dir(project.layout.projectDirectory.dir("src/generated-sources"))
  args = ['gen:sources']
}


tasks.register('buildFrontend', PnpmTask) {
  dependsOn generateContract
  inputs.files(fileTree("src/"))
  outputs.dir(project.layout.buildDirectory.dir("vite"))
  args = ['build']
  environment = System.getenv() + [
    "VITE_TAG": project.version,
    // add git commit
    "VITE_COMMIT": ""
  ]
}

sourceSets {
  main {
    resources {
      srcDir "build/vite"
    }
  }
}

processResources.dependsOn buildFrontend
compileJava.dependsOn buildFrontend




