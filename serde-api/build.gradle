plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
}

tasks.register('sourceJar', Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allJava
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

dependencies {
    implementation libs.kafka.clients
}

artifacts {
    archives sourceJar, javadocJar
}

if (release) {
    signing {
        useInMemoryPgpKeys(
                System.getenv("GPG_PRIVATE_KEY"),
                System.getenv("GPG_PASSPHRASE")
        )
        sign(publishing.publications)
    }
}

publishing {
    if (release) {
        repositories {
            maven {
                url "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2"
                credentials {
                    username System.getenv("NEXUS_USERNAME")
                    password System.getenv("NEXUS_PASSWORD")
                }
            }
        }
    }

    publications {
        maven(MavenPublication) {
            groupId = 'io.kafbat.ui'
            artifactId = 'serde-api'
            version = version

            from components.java

            artifact(sourceJar) {
                classifier = 'sources'
            }

            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            pom {
                name = 'kafbat-ui-serde-api'
                description = 'kafbat-ui-serde-api'
                url = 'http://github.com/kafbat/kafka-ui'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'Kafbat'
                        name = 'Kafbat'
                        organization = 'Kafbat'
                        email = 'maintainers@kafbat.io'
                    }
                }

                scm {
                    connection = 'scm:git://github.com/kafbat/kafka-ui.git'
                    developerConnection = 'scm:git:ssh://github.com:kafbat/kafka-ui.git'
                    url = 'https://github.com/kafbat/kafka-ui'
                }
            }
        }
    }
}
